ext {
    dbSettings = [
            url : System.getenv('MYSQL_URL') ?: 'jdbc:mysql://127.0.0.1:13306/sample?useSSL=false',
            user : System.getenv('MYSQL_USER') ?: 'docker',
            password : System.getenv('MYSQL_PASSWORD') ?: 'docker',
    ]
}

tasks.register('mergeTestReports', TestReport) {
    group = 'report'
    destinationDirectory = layout.buildDirectory.dir('reports/allTests')
    testResults.from subprojects
        .findAll {
            file("${it.buildDir}/reports/tests").exists()
        }
        .collect {
            it.tasks.withType(Test)*.binaryResultsDirectory
        }
        .flatten()
}

tasks.register('generateYamlFromCue', Exec) {
    def cueDir = "${rootDir}/appApiSchema"
    it.inputs.dir(cueDir)
    it.outputs.file("${cueDir}/build/openapi.yaml")
    workingDir cueDir
    commandLine 'cue', 'export', '.', '-o', 'build/openapi.yaml', '-f'
    doFirst {
        mkdir "${cueDir}/build"
    }
}
